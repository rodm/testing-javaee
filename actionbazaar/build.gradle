
plugins {
    id 'war'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'com.actionbazaar'
version = '1.0-SNAPSHOT'

ext {
    glassfish4Home = project.findProperty('glassfish4.home')
    jbossEap7Home = project.findProperty('jbosseap70.home')
    wildfly11Home = project.findProperty('wildfly11.home')
}

sourceSets {
    test {
        resources {
            srcDir('src/main/webapp')
        }
    }
}

dependencies {
    providedCompile group: 'javax', name: 'javaee-api', version: '7.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.jboss.arquillian.junit', name: 'arquillian-junit-container', version: '1.2.0.Final'
    testCompile group: 'org.jboss.shrinkwrap.resolver', name: 'shrinkwrap-resolver-depchain', version: '3.1.2'
    testCompile group: 'org.jboss.arquillian.extension', name: 'arquillian-drone-webdriver-depchain', version: '2.5.0'
    testCompile group: 'org.arquillian.container', name: 'arquillian-container-chameleon', version: '1.0.0.Beta3'
}

war {
    archiveName 'actionbazaar.war'
}

task testGlassFish41(type: Test, dependsOn: testClasses) {
    description = 'Runs the tests using GlassFish 4.1'
    group = 'verification'

    classpath += files('src/test/resources-glassfish')

    environment 'JAVA_HOME', System.properties['java.home']
    systemProperty 'arquillian.launch', 'glassfish41'
    systemProperty 'glassfish.home', glassfish4Home
}

task testJBossEAP7(type: Test, dependsOn: testClasses) {
    description = 'Runs the tests using JBoss EAP 7.0'
    group = 'verification'

    classpath += files('src/test/resources-jboss')

    systemProperty 'arquillian.launch', 'jbosseap70'
    systemProperty 'jboss.home', jbossEap7Home
}

task testWildFly11(type: Test, dependsOn: testClasses) {
    description = 'Runs the tests using WildFly 11.0'
    group = 'verification'

    classpath += files('src/test/resources-jboss')

    systemProperty 'arquillian.launch', 'wildfly11'
    systemProperty 'jboss.home', wildfly11Home
}
